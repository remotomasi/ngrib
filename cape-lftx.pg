#!/usr/bin/gnuplot -persist
reset

set terminal pngcairo truecolor
set termoption dashed
set term pngcairo size 1024,600 #800 pixels by 600 pixels

data = 'final.csv'
set datafile separator ","
# Retrieve statistical properties for LFTX
stats data using 1:'LFTX - surface'
set timefmt "%Y-%m-%d %H:%M"
stats [time(0):time(0) + 5*24*60*60] 'final.csv' u (timecolumn(1)):'LFTX - surface'
min_yL = STATS_min_y
min_pos_yL = STATS_pos_min_y

# Retrieve statistical properties for CAPE
stats data using 1:'CAPE - surface'
set timefmt "%Y-%m-%d %H:%M"
stats [time(0):time(0) + 5*24*60*60] 'final.csv' u (timecolumn(1)):'CAPE - surface'
max_yCP = STATS_max_y
max_pos_yCP = STATS_pos_max_y

# Retrieve statistical properties for CIN
stats data using 1:'CIN - surface'
set timefmt "%Y-%m-%d %H:%M"
stats [time(0):time(0) + 5*24*60*60] 'final.csv' u (timecolumn(1)):'CIN - surface'
min_yCIN = STATS_min_y
min_pos_yCIN = STATS_pos_min_y
max_yCIN = STATS_max_y
max_pos_yCIN = STATS_pos_max_y

set xdata time
set timefmt "%Y-%m-%d %H"
set xrange [time(0):time(0) + 5*24*60*60]
set format x "h%H\n%d/%m\n%a"
set xlabel "Time"
set xtics font ", 10"

#set x2range [0:100]
#set link x
#set x2tics font ",8"
#set format x2 "%s" time
#unset x2tics
#set format x2 "%D\n%R" time
#set xtics format "%tH:%tM:%tS" time

set autoscale y
set autoscale y2
set ylabel 'J/Kg' offset 2.0, 0 textcolor rgb "orange"
set y2tics 1
set y2tics offset -1,0
#set y2range [-6:15]
set y2label '°K' textcolor rgb "purple"
set y2label offset -4,0

run(n) = sprintf("%d",n)
coord(n) = sprintf("%g",n)

set title "Weather Forecast GFS Model - NOAA (Cape-LiftedIndex-Cin) RUN:".run(run)."z -"."Lat: ".coord(lat)." - "."Lon: ".coord(lon)
set key lmargin
set key font ",10"
set grid
set size 1, 1 # ratio 1:1

set style data lines
# Set linestyle 1 to blue (#0060ad)
set style line 1 linecolor rgb '#0060ad' linetype 1 linewidth 2 pointtype 7 pointsize 1
set boxwidth 0.95 relative
set style fill transparent solid 0.7 noborder

set multiplot
set lmargin screen 0.2
set rmargin screen 0.94 

set label 1 sprintf("  < %4.4g J/Kg",max_yCP) at first max_pos_yCP, first max_yCP left tc rgb "dark-orange"
set label 3 sprintf("< %4.4g °K",min_yL) at first min_pos_yL, second min_yL left tc rgb "dark-blue"

# CIN multiplied by -1 since it is a negative area
plot "final.csv" using 1:($122) w boxes lc rgb "orange" fs transparent solid 0.3 border -1 title "CAPE" axes x1y1, \
"" u 1:(max_yCP) lc rgb "orange" dt 3 lw 2 notitle axes x1y1, \
"" u 1:(max_yCP>=500?500:1/0) lc rgb "orange" dt 2 lw 1 notitle, \
"" u 1:(max_yCP>=1000?1000:1/0) lc rgb "orange" dt 2 lw 1 notitle axes x1y1, \
"" u 1:(max_yCP>=2000?2000:1/0) lc rgb "orange" dt 2 lw 1 notitle axes x1y1, \
"" u 1:($121) lc "purple" title "LFTX" axes x1y2, \
"" u 1:(min_yL) lc rgb "purple" dt 3 lw 2 notitle axes x1y2, \
"" u 1:(min_yL<=-2?-2:1/0) lc rgb "purple" dt 2 lw 1 notitle axes x1y2, \
"" u 1:(min_yL<=-4?-4:1/0) lc rgb "purple" dt 2 lw 1 notitle axes x1y2, \
"" u 1:(min_yL<=-6?-6:1/0) lc rgb "purple" dt 2 lw 1 notitle axes x1y2, \
0 dt 1 lc rgb "black" lw 1 axes x1y2 notitle, \
"" u 1:(0):($121) with filledcurves above y2=0 lc rgb "purple" notitle axes x1y2 


### Second plot

unset ylabel
unset y2label
unset ytics
unset y2tics
set ytics 25
set yrange [min_yCIN:0]
set ytics offset  -5, 0
set key b 
set ylabel "CIN" offset  -3.5, 0 textcolor rgb "red"
set rmargin screen 0.94 

unset label 3

set label 2 sprintf("< %3.3g J/Kg",min_yCIN) at first min_pos_yCIN, first min_yCIN left tc rgb "red"
#set label 3 sprintf("< %3.3g J/Kg",max_yCIN) at first max_pos_yCIN, first max_yCIN left tc rgb "brown"set label 10 '4' at screen 0.975, first 4 front tc rgb "light-green"
set label 4 '-20' at screen 0.96, first -20 front tc rgb "blue"
set label 5 '-60' at screen 0.96, first -60 front tc rgb "cyan"
set label 6 '-100' at screen 0.96, first -100 front tc rgb "green"

plot "final.csv" using 1:($123) w boxes lc rgb "red" title "CIN inh." axes x1y1, \
"" u 1:($123 < -100?$123:1/0) w boxes lc rgb "green" title "CIN dis." axes x1y1, \
"" u 1:(min_yCIN<=-20?-20:1/0) lc rgb "blue" dt 2 lw 1 notitle, \
"" u 1:(min_yCIN<=-50?-60:1/0) lc rgb "cyan" dt 2 lw 1 notitle, \
"" u 1:(min_yCIN<=-90?-100:1/0) lc rgb "green" dt 2 lw 1 notitle

unset multiplot

