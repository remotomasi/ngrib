#!/usr/bin/gnuplot -persist
reset
set terminal png
set term png size 1024,600 #800 pixels by 600 pixels

set datafile separator " "
set xdata time
set timefmt "%Y-%m-%d %H"
set xrange [time(0):time(0) + 5*24*60*60]
set format x "h%H\n%d/%m\n%a"
set xlabel "Time"
set xtics font ", 10"

set autoscale y
set autoscale yfixmin
#set ytics 0.05
#set yrange [0:12]
set ylabel 'mm/h'
set y2tics 10
set y2range [0:100]
set y2label '% Chance'

set title "Weather Forecast GFS Model - NOAA (Rain-Frozen/Freezing-Snow)"
set key lmargin
set key font ",10"
set grid
set size 1, 1 # ratio 1:1

set style data lines
set boxwidth 0.95 relative
#set style fill transparent solid 0.5 noborder

plot "final3.csv" using 1:17 w boxes lc rgb "blue" fs solid border -1 title "PrMM/h" axes x1y1, \
"" using 1:18 with filledcurve lc rgb "purple" title "TotPr" axes x1y1, \
"" using 1:16 lc rgb "brown" title "CPrMM/h" axes x1y1, \
"" using 1:19 lc rgb "orange" title "CPrTot" axes x1y1, \
"" using 1:23 lc rgb "red" title "FP%" axes x1y2, \
"" using 1:21 lc rgb "grey" title "F01" axes x1y1, \
"" using 1:20 lc rgb "green" title "R01" axes x1y1, \
"" using 1:24 lc rgb "yellow" title "S01" axes x1y1, \
"" using 1:25 lc rgb "olive" title "SDepth" axes x1y1, \
"" using 1:22 lc rgb "black" title "IceP01" axes x1y1, \
1 lc rgb "#5F9EA0" lw 2 notitle, \
2.5 lc rgb "#5F9EA0" lw 2 notitle
#, \
#10 lc rgb "#5F9EA0" lw 2 notitle

